import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.starter.config'
    id 'com.starter.versioning'
}

commonConfig {
    javaFilesAllowed false
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        def kotlinVersion = plugins.getPlugin("kotlin").kotlinPluginVersion
        configurations.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion kotlinVersion
                }
            }
        }
    }
    tasks.withType(Test).configureEach {
        doLast {
            Thread.sleep(2000) // https://github.com/gradle/gradle/issues/16603
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            apiVersion = "1.4"
            languageVersion = "1.4"
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_1_8.toString()
        targetCompatibility = JavaVersion.VERSION_1_8.toString()
    }

    pluginManager.withPlugin("java") {
        java {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(16))
            }
        }

        if (project.hasProperty("skipJarVersion")) {
            tasks.named("jar") {
                archiveFile.set(new File(project.buildDir, "libs/${project.name}.jar"))
            }
        }
    }
}
